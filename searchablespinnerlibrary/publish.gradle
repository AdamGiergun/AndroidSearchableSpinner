apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


def gitUrl = 'https://github.com/henriquebk1/AndroidSearchableSpinner.git'   // Git repository URL

version '1.0.0'
group 'tech.hibk'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/app-release.aar")
            groupId
            artifactId 'searchablespinnerlibrary'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'searchablespinnerlibrary'
        description = "An Android Searchable Spinner and Dialog"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        dryRun = true
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}